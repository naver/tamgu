# Tamgu에서의 Fiber

파이버는 가볍고 실행 가능한 스레드입니다. 스레드와 마찬가지로, 파이버는 동일한 주소 공간을 공유합니다. 그러나 파이버는 협력적인 멀티태스킹을 사용하고, 스레드는 선점적인 멀티태스킹을 사용합니다. 스레드는 종종 커널의 스레드 스케줄러에 의존하여 바쁜 스레드를 선점하고 다른 스레드를 재개하는 반면, 파이버는 실행 중인 다른 파이버로 자신을 양보합니다.

Tamgu에서는 파이버가 Taskell 함수 위에 구현되어 있습니다. 이러한 함수는 파이버로 실행되기 위해 반복을 포함해야 합니다. 실제로 yield 함수는 존재하지 않습니다.

`fibre` 타입은 다음과 같은 메서드를 노출합니다:

1. `run()`: 기록된 파이버를 실행합니다.
2. `block()`: 파이버는 연결된 체인에 저장되며, 이 체인은 시작부터 끝까지 반복됩니다. 새로운 파이버가 이 목록에 추가되면 새로운 끝점 요소가 됩니다. `block()`은 현재 끝점 요소를 반복 경계로 정의하는 방법입니다. 새로운 파이버는 아직 추가될 수 있지만, `unblock()`이 호출되기 전까지 실행되지 않습니다. 만약 `block()`이 다른 현재 끝점 요소와 함께 다시 호출되면, 이전 경계는 새로운 끝점 요소로 이동합니다.
3. `unblock()`: 반복에 대한 제한을 해제합니다.

파이버를 선언하기 위해서는 먼저 Taskell 함수를 구현한 다음, 이 Taskell 함수를 파이버 변수에 저장해야 합니다.

```tamgu
<MyFunc(a1, a2, ..., an) = ....>
fibre f = MyFunc;
```

새로운 파이버를 기록하기 위해서는 간단히 파라미터와 함께 호출하면 됩니다:

```tamgu
f(p1, p2, ..., pn);
```

만약 `MyFunc`가 반복을 포함하지 않는다면, 자동으로 실행될 것입니다. 출력을 저장해야 하는 경우, 스트림 연산자를 사용할 수 있습니다.

```tamgu
vector v;
v.push(ivector());
v[-1] <<< f(200, 210);
```

파이버를 종료하는 방법은 두 가지입니다: 반복자가 끝에 도달하거나 `break` 명령문이 호출됩니다:

```tamgu
<MyFunc(x, y) : if (x == 10) break else (x + 1)...>
```

파이버는 스레드 내에서 실행될 수 있지만, 한 번에 한 개의 파이버만 하나의 스레드에서 `run()`을 실행할 수 있습니다. 그러나 스레드는 병렬로 새로운 파이버를 기록할 수 있습니다.

## 예제

```tamgu
vector v;
int i;
// 결과를 저장할 정수 벡터를 초기화합니다...
for (i in <3>)
    v.push(ivector());
    
//-----------------------------------------------------------------------
function compute(int i, int x, int y) {
    return i + x + y;
}

<myfiber(x, y) = compute(i, x, y) | i <- [x..y]>
// myfiber로 파이버 변수를 초기화합니다.
fibre f = myfiber;
//-----------------------------------------------------------------------
// 기록은 스레드로부터 수행됩니다...
joined thread recording(int i, int x, int y) {
    println("Fiber:", i);
    v[i] <<< f(x, y);
}

function running() {
    f.run();
}
//-----------------------------------------------------------------------
// 파이버는 스레드로부터 시작됩니다...
int n;
for (i in <0, 60, 20>) {
    recording(n, i, i + 19);
    n++;
}
// 모든 파이버의 기록이 완료될 때까지 대기합니다.
waitonjoined();
//-----------------------------------------------------------------------
// 파이버를 실행합니다...
running();
```